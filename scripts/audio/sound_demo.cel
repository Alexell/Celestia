#  --------------------------------------------------------------------------------------------------
# This file demonstrates the 'play' command
# First patch for audio support in Celestia by Victor (vhpgomes).
# Rewritten by Pirogronian (added virtual common interface and basic implementation in Qt)
# First patch information: https://celestia.space/forum/viewtopic.php?t=8624
#
#  This command accepts 5 arguments:
#
#  play { channel [int] volume [float] loop [bool] nopause [bool] filename [str] }
#
#  channel - specifies the channel that'll be affected by this call.
#            If not given, will assume the default value of 0, You can provide any number between 0 and 7.
#            Any number out of this range will cause the script to fail.
#
#  volume  - specifies the new volume of this channel. If not given, the volume of the channel will be set to 1.0.
#            It starts with the volume 1.0. You can specify any number between 0.0 (minimum, mute) and 1.0 (maximum).
#            If the number is bigger than 1.0, the volume will be set to the maximum volume, 1.0.
#
#  loop    - specifies if the sound file will loop or not.
#            It starts with looping off. The value true turns the looping on. Any other value turns the looping off.
#
#  nopause - parameter that allows a particular sound channel to be paused or not paused on any channel when the spacebar is pressed.
#            Value false - allow pause by spacebar, value true - disallow.
#
#  filename- name of the file that will be played. If only the file name is specified, then the subfolder "sounds" is searched.
#            I.e. in this case the path is relative to the directory "Celestia\sounds". 
#            If the file is located in a different folder, you must specify the full path to it.
#  --------------------------------------------------------------------------------------------------

{
print { origin "bottomleft" row -5 column 3 duration 5 text "Welcome!\n\nYou start an audio playback script." }
wait { duration 5 }

play { channel 0 volume 1 loop true nopause true filename "music.mp3" }

#Since we used the default values for some parameters, we could use the abbreviated version of the command:
#play { channel 0 filename nopause "music.ogg" }

wait { duration 2 }
print { origin "bottomleft" row -3 column 3 duration 5 text "Background music playing (channel 0)." }
wait { duration 8 }


play { channel 1 loop false nopause false filename "comment.mp3" }

wait { duration 2 }
print { origin "bottomleft" row -3 column 3 duration 5 text "Voice comments (channel 1)." }
wait { duration 5 }

print { origin "bottomleft" row -3 column 3 duration 5 text "Simultaneously, you can play any number of channels." }
wait { duration 7 }
print { origin "bottomleft" row -3 column 3 duration 8 text "Now smoothly reduce the volume of the channel 0." }

#Now smoothly reduce the volume level of channel 0, over which we have background music.
play { channel 0 volume 0.8 }
wait { duration 2 }
play { channel 0 volume 0.6 }
wait { duration 2 }
play { channel 0 volume 0.4 }
wait { duration 2 }
play { channel 0 volume 0.2 }
wait { duration 2 }
play { channel 0 volume 0.1 }
wait { duration 2 }

#Before the script ends, you need to stop the playback of the running channels (filename = "").
play { channel 0 filename "" }
play { channel 1 filename "" }


print { origin "bottomleft" row -5 column 2 duration 5 text "End of script..." }
wait { duration 5 }

}
